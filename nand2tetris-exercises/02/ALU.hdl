// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // Apply transformations to x.
        Zero16(a=x, shouldZero=zx, out=x1);
        Neg16(a=x1, shouldNegate=nx, out=x2);

        // Apply transformations to y.
        Zero16(a=y, shouldZero=zy, out=y1);
        Neg16(a=y1, shouldNegate=ny, out=y2);

        // Function selection.
        DMux16(in=x2, sel=f, a=xToAnd, b=xToAdd);
        DMux16(in=y2, sel=f, a=yToAnd, b=yToAdd);

        // Compute both Add and And.
        Add16(a=xToAdd, b=yToAdd, out=addResult);
        And16(a=xToAnd, b=yToAnd, out=andResult);

        // Computation result output.
        Or16(a=addResult, b=andResult, out=postOut);

        // Not the output, if specified.
        Not16(in=postOut, out=notPostOut);
        Mux16(a=postOut, b=notPostOut, sel=no, out=finalOut);
        Or16(a=finalOut, b[0..15]=false, out=out); // This is a workaround to `out` not being usable as an internal input pin.

        // Is the output zero?
        IsZero16(in=finalOut, isZero=zr);

        // Is the output negative?
        // With two's complement representation, we can check if a given number
        // is negative by just checking if the first bit is a 1.
        IsNegative16(in=finalOut, isNegative=ng);
}